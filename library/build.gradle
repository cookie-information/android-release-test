plugins {
  id "com.android.library"
  id 'kotlin-android'
  id 'kotlin-kapt'
  //Add maven-publish and signing plugins
  id 'maven-publish'
  id 'signing'
}

android {
  namespace 'com.cookieinformation.mobileconsents'
  compileSdk 33

  defaultConfig {
    minSdk 21
    targetSdk 33
    versionCode 1
    versionName "1.0.0"
  }

  buildTypes {
    release {
      debuggable false
      minifyEnabled false
      buildConfigField("String", "BASE_URL_TOKEN", "\"https://consent-api.app.cookieinformation.com/oauth2/token\"")
      buildConfigField("String", "BASE_URL_CONSENT_SOLUTION", "\"https://cdnapi-prod.azureedge.net/v1/\"")
      buildConfigField("String", "BASE_URL_CONSENT", "\"https://consent-api.app.cookieinformation.com/v1\"")
    }
    debug {
      debuggable true

      buildConfigField("String", "BASE_URL_TOKEN", "\"https://consent-api.app.cookieinformation.com/oauth2/token\"")
      buildConfigField("String", "BASE_URL_CONSENT_SOLUTION", "\"https://cdnapi-prod.azureedge.net/v1/\"")
      buildConfigField("String", "BASE_URL_CONSENT", "\"https://consent-api.app.cookieinformation.com/v1\"")
    }
  }

  lint {
    lintConfig rootProject.file("lint.xml")

    htmlReport false
    xmlReport true
    xmlOutput rootProject.file("build/reports/lint/lint-results.xml")

    textReport true
    textOutput file('stdout')
    explainIssues false

    // Full linting is run as a part of the CI so it can be skipped for the 'assemble' task.
    checkReleaseBuilds false
  }

  buildFeatures {
    buildConfig true
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  kotlinOptions {
    jvmTarget = '1.8'
    freeCompilerArgs += [
        "-progressive",
        "-Xopt-in=kotlin.RequiresOptIn",
        "-Xexplicit-api=strict"
        ]
  }
}

dependencies {
  api "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  // define a BOM and its version
  implementation(platform("com.squareup.okhttp3:okhttp-bom:4.10.0"))
  // define any required OkHttp artifacts without version
  implementation("com.squareup.okhttp3:okhttp")
  implementation("com.squareup.okhttp3:logging-interceptor")
  implementation "androidx.appcompat:appcompat:1.5.1"
  implementation "androidx.constraintlayout:constraintlayout:2.1.4"
  implementation "androidx.fragment:fragment-ktx:1.5.5"
  implementation "androidx.recyclerview:recyclerview:1.2.1"
  implementation "com.squareup.moshi:moshi:1.14.0"
  kapt "com.squareup.moshi:moshi-kotlin-codegen:1.14.0"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"
  implementation 'com.google.android.material:material:1.7.0'
}

ext {
  PUBLISH_GROUP_ID = 'com.cookieinformation'
  PUBLISH_VERSION = '1.0.0'
  PUBLISH_ARTIFACT_ID = 'mobileconsents'
  PUBLISH_DESCRIPTION = 'SDK for easy user consent managment.'
  PUBLISH_URL = 'https://github.com/cookie-information/android-release'
  PUBLISH_LICENSE_NAME = 'The Apache Software License, Version 2.0'
  PUBLISH_LICENSE_URL =
      'https://www.apache.org/licenses/LICENSE-2.0.txt'
  PUBLISH_DEVELOPER_ID = 'HannaCookieInformation'
  PUBLISH_DEVELOPER_NAME = 'Hanna Shulman'
  PUBLISH_DEVELOPER_EMAIL = 'hsc@cookieinformation.com'
  PUBLISH_SCM_CONNECTION =
      'scm:git:github.com/cookie-information/android-release.git'
  PUBLISH_SCM_DEVELOPER_CONNECTION =
      'scm:git:github.com/cookie-information/android-release.git'
  PUBLISH_SCM_URL =
      'https://github.com/cookie-information/android-releas/tree/master'
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"
